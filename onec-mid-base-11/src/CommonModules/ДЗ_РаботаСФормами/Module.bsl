#Область ПрограммныйИнтерфейс
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ВставитьПолеДЗ_СогласованнаяСкидка(Форма);
		ВставитьПолеДЗ_СтатусЗаказа(Форма);
	ИначеЕсли
		ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли 
		ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли 
		ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма);
	ИначеЕсли
		ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ЭлементКонтактноеЛицо = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма)
	
	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры

Процедура ВставитьПолеДЗ_СогласованнаяСкидка(Форма) Экспорт
	
	ГруппаПересчетСкидки = Форма.Элементы.Добавить("ГруппаПересчетСкидки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаПересчетСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПересчетСкидки.ОтображатьЗаголовок = Ложь;
	ГруппаПересчетСкидки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ЭлементСогласованнаяСкидка = Форма.Элементы.Добавить("ДЗ_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаПересчетСкидки);
	ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
	ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ДЗ_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитатьТаблицу = Форма.Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитатьТаблицу.Заголовок = "Пересчитать таблицу";
	КомандаПересчитатьТаблицу.Действие = "ДЗ_ПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаПересчетСкидки);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
КонецПроцедуры

Процедура ВставитьПолеДЗ_СтатусЗаказа(Форма) Экспорт
	
	ЭлементСтатусЗаказа = Форма.Элементы.Добавить("СтатусЗаказа", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ЭлементСтатусЗаказа.ПутьКДанным = "Объект.ДЗ_СтатусЗаказа";
	ЭлементСтатусЗаказа.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры
#КонецОбласти

